apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

ext {
    versionRelease = "1.0.2"
    versionSnapshot = "1.0.3-SNAPSHOT"
    artifactGroup = "com.kaspersky.android-components"
    artifact = "kaspresso"
    repository = "Kaspresso"
    repositoryName = "Kaspresso"
    gitUrl = "https://github.com/KasperskyLab/Kaspresso.git"
}

publishing {
    publications {
        Kaspresso(MavenPublication) {
            afterEvaluate {
                artifact "$rootDir/kaspresso/build/outputs/aar/kaspresso-release.aar"
                artifact sourcesJar
                artifact javadocJar
            }

            groupId artifactGroup
            artifactId artifact
            version versionRelease

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0]
                        ?: asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }

        KaspressoSnapshot(MavenPublication) {
            afterEvaluate {
                artifact "$rootDir/kaspresso/build/outputs/aar/kaspresso-release.aar"
                artifact sourcesJar
                artifact javadocJar
            }

            groupId artifactGroup
            artifactId artifact
            version versionSnapshot

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0]
                        ?: asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user "$bintrayuser"
    key "$bintraykey"
    publications 'Kaspresso'

    pkg {
        repo = repository
        name = repositoryName
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl

        version {
            name = versionRelease
        }
    }
}

artifactory {
    contextUrl = "https://oss.jfrog.org/artifactory"
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "${bintrayuser}"
            password = "${artifactoryPassword}"
            maven = true
        }

        defaults {
            publications 'KaspressoSnapshot'
        }
    }

    resolve {
        repository {
            repoKey = 'libs-snapshot'
            username = "${bintrayuser}"
            password = "${artifactoryPassword}"
            maven = true
        }
    }
}

bintrayUpload.dependsOn assemble
artifactoryPublish.dependsOn assemble
