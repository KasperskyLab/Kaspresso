apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: '../dependencies.gradle'
apply from: '../dokka.gradle'
apply from: '../publish.gradle'

android {
    compileSdkVersion Integer.parseInt(versions.compileSdk)
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionName "$pKaspressoMajor.$pKaspressoMinor.$pKaspressoBuild"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            main.res.srcDirs += 'src/main/res'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    api "com.kaspersky.android-components:adbserver-device:$versions.adbserver"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    testImplementation "junit:junit:$versions.junit"

    implementation "androidx.test:runner:$versions.runner"
    implementation "androidx.test.espresso:espresso-web:$versions.espresso"
    implementation "androidx.test.espresso:espresso-core:$versions.espresso"
    implementation "androidx.test.espresso:espresso-contrib:$versions.espresso"
    implementation "androidx.test.espresso:espresso-intents:$versions.espresso"
    implementation "androidx.test.uiautomator:uiautomator:$versions.uiautomator"
    implementation "com.agoda.kakao:kakao:$versions.kakao"
    implementation "com.google.code.gson:gson:$versions.gson"
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

artifactory {
    contextUrl = "https://oss.jfrog.org/artifactory"
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "${bintrayuser}"
            password = "${artifactoryPassword}"
            maven = true
        }

        defaults {
            publications 'Kaspresso'
        }
    }

    resolve {
        repository {
            repoKey = 'libs-snapshot'
            username = "${bintrayuser}"
            password = "${artifactoryPassword}"
            maven = true
        }
    }
}
