apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

ext {
    ver = '1.0.0'
    groupId = 'com.kaspersky.android-components'
    artifactId = 'kaspresso'
    repo = 'Kaspresso'
    repoName = 'Kaspresso'
    vcsUrl = 'https://github.com/KasperskyLab/Kaspresso.git'
}

publishing {
    publications {
        Kaspresso(MavenPublication) {
            artifact "$rootDir/kaspresso/build/outputs/aar/kaspresso-release.aar"
            artifact sourcesJar
            artifact javadocJar
            groupId this.groupId
            artifactId this.artifactId
            version this.ver

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0]
                        ?: asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = property('bintrayuser')
    key = property('bintraykey')
    publications = ['Kaspresso']

    pkg {
        repo = this.repo
        name = this.repoName
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = this.vcsUrl

        version {
            name = this.ver
        }
    }
}

bintrayUpload.dependsOn assemble
